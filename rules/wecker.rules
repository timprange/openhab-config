var Timer timerAlarm = null

rule "Alarm Clock"
when
    Item Tim_Wecker_Timestamp changed
then
    if (Tim_Wecker_Timestamp.state as Number == 0) {
        if (timerAlarm !== null) {
            timerAlarm.cancel
            timerAlarm = null
        }
        logInfo("alarm", "All alarms are cancelled")
    } else {
        var epoch = new DateTime((Tim_Wecker_Timestamp.state as Number).longValue)
        logInfo("alarm", "Scheduling alarm for " +  epoch.toString)

        if (timerAlarm !== null) {
            logInfo("alarm", "Reschedule alarm")
            timerAlarm.reschedule(epoch)
        } else {
            logInfo("alarm", "New Alarm")
            timerAlarm = createTimer(epoch,
                [|
                    // Turn on stuff, e.g. radio or light
                    Bad_Heizung_Soll_Proxy.sendCommand(23);
                    logInfo("alarm", "alarm is expired")
                ]
            )
        }
    }
end